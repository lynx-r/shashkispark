buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        flatDir {
            dirs 'lib'
        }
    }
}

// lombok configuration
plugins {
    id 'net.ltgt.apt' version '0.15'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'

    group = 'com.workingbit'
}

subprojects { subproject ->
    if (subproject.name.endsWith('service')) {
        subproject.apply plugin: 'application'
        subproject.apply plugin: 'war'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    configurations {
        runtime.exclude group: 'org.eclipse.jetty.websocket'
        runtime.exclude group: 'org.eclipse.jetty', module: 'jetty-client'
        runtime.exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
        runtime.exclude group: 'org.eclipse.jetty', module: 'jetty-xml'
        runtime.exclude group: 'org.eclipse.jetty', module: 'jetty-io'
    }

    test.onlyIf { Boolean.getBoolean('skip.tests') }

    task deploylocal(type: Copy) {
        from libsDir
        include '*.war'
        into "${tomcatHome}/webapps"
        rename { fileName ->
            fileName.replace("service-${version}", "")
        }
    }

    build.dependsOn deploylocal

    [compileJava, compileTestJava]*.options.collect {options ->
        options.compilerArgs.add '-Xlint:unchecked'
    }
}

configure(subprojects) {
    dependencies {
        compile 'org.apache.commons:commons-lang3:3.5'
        compile 'org.apache.commons:commons-collections4:4.1'

        compile('com.sparkjava:spark-core:2.7.2')

        testCompile group: 'junit', name: 'junit', version: '4.+'

        // test spark controllers
        testCompile "com.despegar:spark-test:1.1.6"
    }
}

configure(subprojects.findAll { it.name.endsWith('service') }) { p ->
    dependencies {

        compile 'io.reactivex:rxjava:1.2.2'

        compile group: 'com.amazonaws.serverless', name: 'aws-serverless-java-container-spark', version: '1.1'

        compile project(':sharedmodule')
    }
}

project(':securityservice') {
    version = '1.0'
    mainClassName = 'com.workingbit.security.SecurityApplication'

    dependencies {
        compile project(':orchestratemodule')
    }
}

project(':articleservice') {
    version = '1.0'
    mainClassName = 'com.workingbit.article.ArticleApplication'

    dependencies {
        compile 'org.ehcache:ehcache:3.5.2'

        compile project(':orchestratemodule')
    }
}

project(':boardservice') {
    version = '1.0'
    mainClassName = 'com.workingbit.board.BoardApplication'

    dependencies {
        // tree
        compile 'com.github.rutledgepaulv:prune:1.3'

        compile 'org.ehcache:ehcache:3.5.2'

        compile project(':orchestratemodule')

        compile files('lib/grammatica-1.6.jar')
    }
}

project(':orchestratemodule') {
    jar {
        baseName = 'orchestratemodule'
        version = "1.0"
    }

    dependencies {
        // https://mvnrepository.com/artifact/org.redisson/redisson
        compile group: 'org.redisson', name: 'redisson', version: '3.6.5'

        compile project(':sharedmodule')
    }
}

project(':sharedmodule') {
    jar {
        baseName = 'sharedmdoule'
        version = "1.0"
    }

    dependencies {
        compile 'commons-codec:commons-codec:1.9'

        compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.150'

        compile 'com.fasterxml.jackson.core:jackson-core:2.8.5'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.1'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.1'
        compile 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.1'

        // https://mvnrepository.com/artifact/org.hibernate.validator/hibernate-validator
        compile group: 'org.hibernate.validator', name: 'hibernate-validator', version: '6.0.9.Final'

        // https://mvnrepository.com/artifact/org.glassfish/javax.el
        compile group: 'org.glassfish', name: 'javax.el', version: '3.0.0'

//        compileOnly('org.projectlombok:lombok:1.16.20')
        compileOnly files('lib/lombok-edge.jar')
        annotationProcessor files('lib/lombok-edge.jar')

        compile 'uk.com.robust-it:cloning:1.9.10'

        compile 'com.mashape.unirest:unirest-java:1.4.9'

        compile 'org.cfg4j:cfg4j-core:4.4.1'

        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile 'ch.qos.logback:logback-classic:1.3.0-alpha4'
    }
}

sourceCompatibility = 1.10
targetCompatibility = 1.10

repositories {
    mavenCentral()
}

//task wrapper(type: Wrapper) {
//    gradleVersion = '4.7'
//}
