buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        flatDir {
            dirs 'lib'
        }
    }
}


allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'

    group = 'com.workingbit'
    version = '1.0'
}

subprojects { subproject ->
    if (subproject.name.endsWith('service')) {
        subproject.apply plugin: 'application'
        subproject.apply plugin: 'war'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile 'org.apache.commons:commons-lang3:3.5'
        compile 'org.apache.commons:commons-collections4:4.1'

        compile "com.sparkjava:spark-core:2.7.2"

        testCompile group: 'junit', name: 'junit', version: '4.+'
    }

    test.onlyIf { Boolean.getBoolean('skip.tests') }
}

configure(subprojects.findAll { it.name.endsWith('service') }) { p ->
    dependencies {
        compile 'io.reactivex:rxjava:1.2.2'

        compile project(':sharedmodule')

        testCompile "com.despegar:spark-test:1.1.6"
    }
}

project(':securityservice') {

    project.ext {
        region = 'eu-west-1'
        s3Bucket = 'securityservice'
        environment = 'prod'
    }

    version = 'v1'
    mainClassName = 'com.workingbit.security.SecurityApplication'

    dependencies {
    }
}

project(':articleservice') {
    version = 'v1'
    mainClassName = 'com.workingbit.article.ArticleApplication'
}

project(':boardservice') {
    version = 'v1'
    mainClassName = 'com.workingbit.board.BoardApplication'

    dependencies {
        // tree
        compile 'com.github.rutledgepaulv:prune:1.3'

        compile 'org.ehcache:ehcache:3.5.2'

        compile files('lib/grammatica-1.6.jar')
    }
}

project(':sharedmodule') {
    jar {
        baseName = 'sharedmdoule'
        version = "${sharemodule_version}"
    }

    dependencies {
        compile 'commons-codec:commons-codec:1.9'

        compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.150'

        compile 'com.fasterxml.jackson.core:jackson-core:2.8.5'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.8.5'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.5'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.1'
        compile 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.9.1'
        compile 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.9.1'

        compile 'org.projectlombok:lombok:1.16.18'

        compile 'uk.com.robust-it:cloning:1.9.5'

        compile 'com.mashape.unirest:unirest-java:1.4.9'

        compile 'org.cfg4j:cfg4j-core:4.4.1'

        compile 'org.slf4j:slf4j-api:1.8.0-beta2'
        compile 'ch.qos.logback:logback-classic:1.3.0-alpha4'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}
